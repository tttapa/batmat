function(koqkatoo_coroutine_flags file)
    if (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
        # https://gcc.gnu.org/bugzilla/show_bug.cgi?id=109867
        set_source_files_properties(${file} PROPERTIES COMPILE_OPTIONS "-Wno-switch-default")
    endif()
endfunction()

function(codegen_cholundate_microkernels tgt)

    add_library(${tgt}-microkernels INTERFACE)
    list(APPEND KOQKATOO_CODEGEN_TARGETS ${tgt}-microkernels)
    set(OUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/${tgt}/codegen")
    set(TPP_DIR "${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../../include/koqkatoo/cholundate/micro-kernels")
    foreach(alg "householder-downdate" "householder-updowndate")
        add_library(${tgt}-impl-${alg} STATIC)
        target_link_libraries(${tgt}-impl-${alg} PRIVATE ${tgt} warnings)
        target_precompile_headers(${tgt}-impl-${alg} PRIVATE "${TPP_DIR}/../${alg}.tpp")
        target_link_libraries(${tgt}-microkernels INTERFACE ${tgt}-impl-${alg})
        list(APPEND KOQKATOO_CODEGEN_TARGETS ${tgt}-impl-${alg})
        foreach (op "diag" "full" "tail")
            add_library(${tgt}-microkernels-${alg}-${op} STATIC)
            target_link_libraries(${tgt}-microkernels-${alg}-${op} PRIVATE ${tgt} warnings)
            target_precompile_headers(${tgt}-microkernels-${alg}-${op} PRIVATE "${TPP_DIR}/${alg}-${op}.tpp")
            target_link_libraries(${tgt}-impl-${alg} PRIVATE ${tgt}-microkernels-${alg}-${op})
            list(APPEND KOQKATOO_CODEGEN_TARGETS ${tgt}-microkernels-${alg}-${op})
        endforeach()
        target_link_libraries(${tgt}-microkernels INTERFACE ${tgt}-impl-${alg})
        foreach(R 32 24 16 12 8 4 2 1)
            configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/${alg}-diag.cpp.in" "${OUT_DIR}/${alg}-diag-${R}.cpp" @ONLY)
            target_sources(${tgt}-microkernels-${alg}-diag PRIVATE "${OUT_DIR}/${alg}-diag-${R}.cpp")
            foreach(S 64 48 40 32 24 16 12 8 4 2 1)
                configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/${alg}-tail.cpp.in" "${OUT_DIR}/${alg}-tail-${R}-${S}.cpp" @ONLY)
                target_sources(${tgt}-microkernels-${alg}-tail PRIVATE "${OUT_DIR}/${alg}-tail-${R}-${S}.cpp")
                configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../${alg}.cpp.in" "${OUT_DIR}/${alg}-${R}-${S}.cpp" @ONLY)
                target_sources(${tgt}-impl-${alg} PRIVATE "${OUT_DIR}/${alg}-${R}-${S}.cpp")
            endforeach()
        endforeach()
        foreach(R RANGE 1 32)
            configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/${alg}-full.cpp.in" "${OUT_DIR}/${alg}-full-${R}.cpp" @ONLY)
            target_sources(${tgt}-microkernels-${alg}-full PRIVATE "${OUT_DIR}/${alg}-full-${R}.cpp")
        endforeach()
    endforeach()

    if (KOQKATOO_WITH_LIBFORK)
        add_library(${tgt}-impl-householder-downdate-libfork STATIC)
        target_link_libraries(${tgt}-impl-householder-downdate-libfork PRIVATE ${tgt} warnings)
        target_precompile_headers(${tgt}-impl-householder-downdate-libfork PRIVATE "${TPP_DIR}/../householder-downdate-libfork.tpp")
        find_package(libfork CONFIG REQUIRED)
        foreach(R 32 24 16 12 8 4 2 1)
            foreach(S 64 48 40 32 24 16 12 8 4 2 1)
                math(EXPR S_R_REM "${S} % ${R}")
                if (S_R_REM EQUAL 0)
                    configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../householder-downdate-libfork.cpp.in" "${OUT_DIR}/householder-downdate-libfork-${R}-${S}.cpp" @ONLY)
                    koqkatoo_coroutine_flags("${OUT_DIR}/householder-downdate-libfork-${R}-${S}.cpp")
                    target_sources(${tgt}-impl-householder-downdate-libfork PRIVATE "${OUT_DIR}/householder-downdate-libfork-${R}-${S}.cpp")
                endif()
            endforeach()
        endforeach()
        target_link_libraries(${tgt}-impl-householder-downdate-libfork PRIVATE libfork::libfork)
        foreach (op "diag" "full" "tail")
            target_link_libraries(${tgt}-impl-householder-downdate-libfork PRIVATE ${tgt}-microkernels-householder-downdate-${op})
        endforeach()
        target_link_libraries(${tgt}-microkernels INTERFACE ${tgt}-impl-householder-downdate-libfork)
        list(APPEND KOQKATOO_CODEGEN_TARGETS ${tgt}-impl-householder-downdate-libfork)
    endif()

    add_library(${tgt}-impl-householder-downdate-static STATIC)
    target_link_libraries(${tgt}-impl-householder-downdate-static PRIVATE ${tgt} warnings)
    target_precompile_headers(${tgt}-impl-householder-downdate-static PRIVATE "${TPP_DIR}/../householder-downdate-static.tpp")
    foreach(R 32 24 16 12 8 4 2 1)
        foreach(S 64 48 40 32 24 16 12 8 4 2 1)
            math(EXPR S_R_REM "${S} % ${R}")
            if (S_R_REM EQUAL 0)
                configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../householder-downdate-static.cpp.in" "${OUT_DIR}/householder-downdate-static-${R}-${S}.cpp" @ONLY)
                koqkatoo_coroutine_flags("${OUT_DIR}/householder-downdate-static-${R}-${S}.cpp")
                target_sources(${tgt}-impl-householder-downdate-static PRIVATE "${OUT_DIR}/householder-downdate-static-${R}-${S}.cpp")
            endif()
        endforeach()
    endforeach()
    foreach (op "diag" "full" "tail")
        target_link_libraries(${tgt}-impl-householder-downdate-static PRIVATE ${tgt}-microkernels-householder-downdate-${op})
    endforeach()
    target_link_libraries(${tgt}-microkernels INTERFACE ${tgt}-impl-householder-downdate-static)
    list(APPEND KOQKATOO_CODEGEN_TARGETS ${tgt}-impl-householder-downdate-static)

    set(KOQKATOO_CODEGEN_TARGETS ${KOQKATOO_CODEGEN_TARGETS} PARENT_SCOPE)
endfunction()
