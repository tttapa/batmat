function(batmat_at_least_O1 tgt)
    set(GCC_ISH $<OR:$<CXX_COMPILER_ID:GNU>,$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_ID:AppleClang>>)
    target_compile_options(${tgt} PRIVATE "$<$<AND:$<CONFIG:Debug>,${GCC_ISH}>:-O1>")
endfunction()

function(batmat_codegen_micro_kernels tgt headers)

    set(SIGNS_gemm "false" "true")
    set(SIGNS_gemm-diag "false" "true")
    set(SIGNS_gemv "false" "true")
    set(SIGNS_symv "false" "true")
    set(SIGNS_syomv "false" "true")
    set(SIGNS_trsm "false")
    set(SIGNS_potrf "false" "true")
    set(SIGNS_trtri "false")

    add_library(${tgt}-micro-kernels INTERFACE)
    list(APPEND BATMAT_CODEGEN_TARGETS ${tgt}-micro-kernels)
    set(OUT_DIR "${CMAKE_CURRENT_BINARY_DIR}/${tgt}/codegen")
    set(TPP_DIR "batmat/linalg/micro-kernels")
    foreach(op "gemm" "gemm-diag" "gemv" "symv" "syomv" "trsm" "potrf" "trtri")
        add_library(${tgt}-micro-kernels-${op} STATIC)
        batmat_at_least_O1(${tgt}-micro-kernels-${op})
        target_link_libraries(${tgt}-micro-kernels-${op} PRIVATE ${headers} warnings)
        target_precompile_headers(${tgt}-micro-kernels-${op} PRIVATE <${TPP_DIR}/${op}.tpp>)
        target_link_libraries(${tgt}-micro-kernels INTERFACE ${tgt}-micro-kernels-${op})
        if (CMAKE_CXX_COMPILER_ID MATCHES "Clang")
            target_compile_options(${tgt}-micro-kernels-${op} PRIVATE "-ffp-contract=fast")
        endif()
        list(APPEND BATMAT_CODEGEN_TARGETS ${tgt}-micro-kernels-${op})
        foreach(DType_ IN LISTS BATMAT_DTYPES)
            string(TOUPPER "${DType_}" DTypeU)
            string(REPLACE "std_" "std::" DType "${DType_}")
            foreach(Negate IN LISTS SIGNS_${op})
                foreach(VL IN LISTS BATMAT_VECTOR_LENGTHS_${DTypeU})
                    set(Namespace "${DType_}_${VL}")
                    configure_file("${CMAKE_CURRENT_FUNCTION_LIST_DIR}/${op}.cpp.in" "${OUT_DIR}/${op}-${DType_}-${VL}-${Negate}.cpp" @ONLY)
                    target_sources(${tgt}-micro-kernels-${op} PRIVATE "${OUT_DIR}/${op}-${DType_}-${VL}-${Negate}.cpp")
                endforeach()
            endforeach()
        endforeach()
    endforeach()

    set(BATMAT_CODEGEN_TARGETS ${BATMAT_CODEGEN_TARGETS} PARENT_SCOPE)
endfunction()
