include(CheckTypeSize)

function(batmat_set_mkl_interface)
    set(MKL_INTERFACE_int32_t "lp64")
    set(MKL_INTERFACE_int64_t "ilp64")
    set(MKL_INTERFACE_int "lp64")
    set(MKL_INTERFACE_long "ilp64")
    set(MKL_INTERFACE_long-long "ilp64")
    set(MKL_INTERFACE_long-long-int "ilp64")
    string(REPLACE " " "-" INDEX_TYPE "${BATMAT_DENSE_INDEX_TYPE}")
    if (NOT DEFINED MKL_INTERFACE_${INDEX_TYPE})
        message(SEND_ERROR "Unsupported index type ${BATMAT_DENSE_INDEX_TYPE}")
    endif()
    set(MKL_INTERFACE "${MKL_INTERFACE_${INDEX_TYPE}}" PARENT_SCOPE)
endfunction()

if (BATMAT_WITH_MKL AND BATMAT_WITH_OPENBLAS)
    message(WARNING "Options BATMAT_WITH_MKL and BATMAT_WITH_OPENBLAS are both enabled. Using MKL and ignoring OpenBLAS.")
endif()

add_library(blas-lapack-lib INTERFACE)
target_compile_definitions(blas-lapack-lib INTERFACE
    $<$<BOOL:${BATMAT_WITH_MKL}>:BATMAT_WITH_MKL>)
if (BATMAT_WITH_MKL)
    set(BATMAT_MKLROOT_DEFINED false)
     if (NOT "${BATMAT_MKLROOT}" STREQUAL "")
        set(BATMAT_MKLROOT_DEFINED true)
        string(REPLACE "$ENV{HOME}" "\$ENV{HOME}"
            BATMAT_MKLROOT_ESCAPED ${BATMAT_MKLROOT})
    endif()
    if (NOT DEFINED MKL_THREADING)
        if (BATMAT_WITH_OPENMP)
            if (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
                set(MKL_THREADING "gnu_thread")
            endif()
        else()
            set(MKL_THREADING "sequential")
        endif()
    endif()
    if (NOT DEFINED MKL_LINK)
        set(MKL_LINK "static")
    endif()
    if (NOT DEFINED MKL_INTERFACE)
        batmat_set_mkl_interface()
    endif()
    set(BATMAT_MKLROOT_DEFINED ${BATMAT_MKLROOT_DEFINED} CACHE INTERNAL "")
    set(BATMAT_MKLROOT_ESCAPED ${BATMAT_MKLROOT_ESCAPED} CACHE INTERNAL "")
    if (BATMAT_MKLROOT_DEFINED AND NOT DEFINED ENV{MKLROOT})
        set(ENV{MKLROOT} "${BATMAT_MKLROOT}")
    endif()
    if (DEFINED ENV{MKLROOT})
        list(PREPEND CMAKE_FIND_ROOT_PATH "$ENV{MKLROOT}")
    endif()
    find_package(MKL CONFIG REQUIRED)
    target_link_libraries(blas-lapack-lib INTERFACE MKL::MKL)
elseif(BATMAT_WITH_OPENBLAS)
    find_package(OpenBLAS REQUIRED)
    target_link_libraries(blas-lapack-lib INTERFACE OpenBLAS::OpenBLAS)
else()
    find_package(BLAS REQUIRED)
    find_package(LAPACK REQUIRED)
    target_link_libraries(blas-lapack-lib INTERFACE BLAS::BLAS LAPACK::LAPACK)
endif()
add_library(batmat::blas-lapack-lib ALIAS blas-lapack-lib)
